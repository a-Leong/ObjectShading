<!DOCTYPE html>
<html lang="en">
    <head>
        <title>Displacement mapping</title>
        <meta charset="utf-8">
        <meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">
        <style>
            body {
                font-family: Monospace;
                background-color: #707080;
                margin: 0px;
                overflow: hidden;
            }
        </style>
    </head>
    <body>
    <div id="container">
        <div id="controlBar" style="text-align: center;">
            <input type="file" id="meshInputFile" style="display: none;" onchange="loadFile(this.files[0])">
            <input type="button" id="meshInputButton" value="Select OBJ File" onclick="document.getElementById('meshInputFile').click()">
            <input type="file" id="texInputFile" style="display: none;" onchange="loadTexture(this.files[0], 'diffuseTexture')">
            <input type="button" id="texInputButton" value="Select Texture File" onclick="document.getElementById('texInputFile').click()">
            <input type="file" id="displacementInputFile" style="display: none;" onchange="loadTexture(this.files[0], 'displacementMap')">
            <input type="button" id="displacementInputButton" value="Select Displacement Map File" onclick="document.getElementById('displacementInputFile').click()">
            <label><input type="checkbox" id="showAxesCheckbox" onchange="toggleAxes(this.checked)" checked="true">Show Axes</label>
            <label><input type="checkbox" id="showWireframeCheckbox" onchange="toggleWireframe(this.checked)">Show Wireframe</label>
            <label><input type="checkbox" id="showNormalsCheckbox" onchange="toggleNormals(this.checked)">Show Normals</label>
            <label><input type="checkbox" id="fixLightsToCameraCheckbox" onchange="toggleFixLightsToCamera(this.checked)">Fix lights to camera</label>
            <br>
            Normal length:
            <input type="range" id="normalLengthRange" min="0.02" max="0.5" step="0.02" value="0.3" oninput="setNormalLength(this.value)">
            Exposure:
            <input type="range" id="exposureRange" min="10.0" max="18.0" step="0.5" value="14.0" oninput="setUniformLog('exposure', this.value)">
            Roughness:
            <input type="range" id="roughnessRange" min="-16.0" max="0.0" step="0.5" value="-8.0" oninput="setUniformLog('roughness', this.value)">
            Displacement scale:
            <input type="range" id="displacementScaleRange" min="-8.0" max="0.0" step="0.25" value="-4.0" oninput="setUniformLog('displacementScale', this.value)">
            <br>
            Left click + drag to orbit. Right click + drag to pan. Scroll to zoom.
        </div>
    </div>

        <script src="js/three.min.js"></script>
        <script src="js/BufferGeometryUtils.js"></script>
        <script src="js/Detector.js"></script>
        <script src="js/OrbitControls.js"></script>
        <script src="js/OBJLoader.js"></script>
<!-----------------------------------------------------------------------------
        VERTEX SHADER
------------------------------------------------------------------------------>
        <script id="vertexShader" type="x-shader/x-vertex">
// Provided by Three.js:
// = object.matrixWorld
// uniform mat4 modelMatrix;

// = camera.matrixWorldInverse * object.matrixWorld
// uniform mat4 modelViewMatrix;

// = camera.projectionMatrix
// uniform mat4 projectionMatrix;

// = camera.matrixWorldInverse
// uniform mat4 viewMatrix;

// = inverse transpose of modelViewMatrix
// uniform mat3 normalMatrix;

// = camera position in world space
// uniform vec3 cameraPosition;

// RenderMesh Input
// attribute vec3 position;
// attribute vec3 normal;
// attribute vec2 uv;

uniform sampler2D displacementMap;
uniform float displacementScale;

varying vec2 vUv; // UV coordinates.
varying vec3 vNormal; // Normal in eye space.
varying vec4 vPosition; // Position in eye space.

void main() {    
    vNormal = normalize((normalMatrix * normal).xyz);
    vPosition = modelViewMatrix * vec4((position), 1.0);
    vUv = uv;

    // Calculate Displacement
    vec3 dispMap = texture2D(displacementMap, vUv).rgb;
    float dispMag = displacementScale * (dispMap.r + dispMap.b + dispMap.g) / 3.0;
    vec3 dispVec = normal.xyz * vec3(dispMag);

    gl_Position = projectionMatrix * modelViewMatrix * vec4((position + dispVec), 1.0);


}
        </script>
<!-----------------------------------------------------------------------------
        FRAGMENT SHADER
------------------------------------------------------------------------------>
        <script id="fragmentShader" type="x-shader/x-fragment">
uniform vec3 lightColors[ NUM_LIGHTS ];
uniform vec3 lightPositions[ NUM_LIGHTS ]; // in eye space

uniform float exposure;
uniform float roughness;

uniform sampler2D diffuseTexture; // k_d, the diffuse color

varying vec2 vUv; // Fragment UV coordinates.
varying vec3 vNormal; // Fragment normal in eye space.
varying vec4 vPosition; // Fragment position in eye space.

vec3 to_sRGB(vec3 c) { return pow(c, vec3(1.0/2.2)); }
vec3 from_sRGB(vec3 c) { return pow(c, vec3(2.2)); }

float PI = 3.1415926535897932384626433832795;

void main() {
    vec3 N = normalize(vNormal);
    vec3 V = normalize(-vPosition.xyz);
    
    vec3 finalColor = vec3(0.0, 0.0, 0.0);

    float m = roughness;

    for (int i = 0; i < NUM_LIGHTS; i++) {
        float r = length(lightPositions[i] - vPosition.xyz);
        vec3 L = normalize(lightPositions[i] - vPosition.xyz); 
        vec3 H = normalize(L + V);

        // Dot products
        float NH = max(dot(N,H),0.0);
        float VH = max(dot(V,H),0.0);
        float NV = max(dot(N,V),0.0);
        float NL = max(dot(N,L),0.0);

        // Fresnel Term
        float F_0 = 0.04;
        float fresnel = F_0 + ((1.0 - F_0) * pow(1.0 - VH,5.0));

        // Mircofacet Term
        float MFExp = (pow(NH,2.0) - 1.0) / (pow(m,2.0) * pow(NH,2.0));
        float microFacet = (1.0 / (pow(m,2.0) * pow(NH,4.0))) * exp(MFExp);

        // Geometric Attenuation
        float geoAtten1 = (2.0 * NH * NV) / VH;
        float geoAtten2 = (2.0 * NH * NL) / VH;
        float G = min(min(geoAtten1,geoAtten2),1.0);

        // Specular Coefficient
        vec3 k_s = vec3(1.0);

        // Diffuse BRDF
        vec3 k_d = from_sRGB(texture2D(diffuseTexture, vUv).rgb) / PI;

        // Cook-Torrance Terms Synthesized
        vec3 cookT = k_s * vec3(fresnel / PI) * vec3((microFacet * G) / (NV * NL)) + k_d;


        finalColor += (lightColors[i] / (r*r)) * NL * cookT;
    }

    gl_FragColor = vec4(to_sRGB(finalColor * exposure), 1.0);
}
        </script>
<!-----------------------------------------------------------------------------
        END SHADERS
------------------------------------------------------------------------------>
        <script src="js/A4Common.js"></script>
        <script>
            commonInit();
            init();
            animate();
            
            function init() {
                defaultTextureTarget = 'diffuseTexture';
                meshMaterial.uniforms.diffuseTexture = { type: 't', value: null };
                meshMaterial.uniforms.displacementMap = { type: 't', value: null };
            
                addLight(new THREE.Vector3(50, 50, 100), new THREE.Vector3(0.8, 0.8, 0.8));
                addLight(new THREE.Vector3(-50, -50, -50), new THREE.Vector3(0.5, 0.5, 0.6));
                
                document.getElementById('roughnessRange').oninput();
                document.getElementById('displacementScaleRange').oninput();
            }
        </script>
    </body>
</html>
